plugins {
    id 'com.android.application' version '8.1.3' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.10' apply false
    id 'org.hidetake.swagger.generator' version '2.14.0'
}

ext {
    gotifyVersion = 'master'
    specLocation = "$layout.buildDirectory/gotify.spec.json"
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

static def download(String url, String filename ) {
    new URL( url ).openConnection().with { conn ->
        new File( filename ).withOutputStream { out ->
            conn.inputStream.with { inp ->
                out << inp
                inp.close()
            }
        }
    }
}

tasks.register('downloadSpec') {
    inputs.property 'version', gotifyVersion
    doFirst {
        layout.buildDirectory.mkdirs()
        download("https://raw.githubusercontent.com/gotify/server/${gotifyVersion}/docs/spec.json", specLocation)
    }
}

swaggerSources {
    gotify {
        inputFile = specLocation as File
        code {
            configFile = "$projectDir/swagger.config.json" as File
            language = 'java'
            outputDir = "$projectDir/client" as File
        }
    }
}

dependencies {
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.3.1'
}

generateSwaggerCode.dependsOn downloadSpec